;+
;===========================================================================
;  NAME: 
;		   bolodarkx_tdrift
; 
;  DESCRIPTION: 
;                  Find measurements in loadcurve structure with low 
;                  temperature drift. Assumes that data are still 
;                  ordered in time.
;
;  USAGE: 
;		   a = bolodarkx_tdrift( x, limit=limit, tfract=tfract)
;
;  INPUT:
;      x           load curve structure as generated by   
;		   bolodark_read_loadcrv.pro		  
;
;  OUTPUT:
;		   function (array of pointers) pointers to elements in x with 
;		   temperature drifts below the limit
;
;  KEYWORDS:
;	limit	   fractional limit for temperature drift		      
;	tfract	   fraction of measurement time that is averaged at	      
;                  the beginning and the end to determine start and end point 
;		   of temperature drift.				      
;
;  AUTHOR: 
;		   Bernhard Schulz
;
;  Edition History
; 
;  Date		Programmer	Remarks
;  2003/05/07 	B.Schulz   	initial test version
;===========================================================================
;-

function bolodarkx_tdrift, x, limit=limit, tfract=tfract, $
				below=below, above=above

if NOT keyword_set(limit)  then limit = 0.05	;fraction of drift
if NOT keyword_set(tfract) then tfract = 0.15	;fraction of measurement time
						;used for averaging start and
						;end value for T_c drift


nf = n_elements(x)
delta = fltarr(nf)
T_c   = fltarr(nf)

for ifile=0, nf-1 do begin

  nt = n_elements((*x[ifile]).T_c)

  if nt GE 2 then begin
  
    n10t = ceil(nt *tfract)  ;inices for fraction of measurement time  
    delta[ifile]= abs(avg((*x[ifile]).T_c[0:n10t]) - avg((*x[ifile]).T_c[nt-1-n10t:nt-1]))
    T_c[ifile]  = avg((*x[ifile]).T_c)

  endif else begin

    delta[ifile]=0	;if only one or two elements in T_c

  endelse

endfor

ixt = where(delta/T_c LT limit) ;get index for good measurements

if keyword_set(above) then begin
  ixt1 = where(T_c[ixt] GT above, cnt)
  if cnt LT 1 then message, 'No values found above '+string(above)
  ixt = ixt[ixt1]
endif

if keyword_set(below) then begin
  ixt1 = where(T_c[ixt] LT below, cnt)
  if cnt LT 1 then message, 'No values found below '+string(below)
  ixt = ixt[ixt1]
endif

return, ixt
end
