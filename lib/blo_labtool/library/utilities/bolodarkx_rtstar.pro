;+
;===========================================================================
;  NAME: 
;		  bolodarkx_rtstar
;  
;  DESCRIPTION: 
;		  Derive R_star and T_star for all channels in 
;	 	  data structure
; 
;
;  USAGE: 
;		  bolodarkx_rtstar, x, R_star, T_star, clabel, slope=slope, plot=plot
;
;  INPUT: 	
;	x	  load curve structure as generated by bolodark_read_loadcrv.pro  
;    
;  OUTPUT:
;	R_star	  (float array) bolometer voltage				  
;	T_star	  (float array) bias voltage					  
;	clabel	  (string array) labels of channels				  
;
;  KEYWORD:
;	plot	  generates plots						  
;	slope	  use slope method to derive Rmin				  
;       Rload	  Load resistance. If set, the Rload value will be used.	  
;     		  Otherwise, the deafult value Rload=2.0e7 will be used.	  
;
;
;  Author: 
;		  Bernhard Schulz
;	
; 
;  Edition History:
;
;  Date     	 Programmer  	Remarks
;  ----------  	 ----------	-------
;  2003-05-08 	 B. Schulz  	initial test version
;  2003-09-19	 L. Zhang  	Add Rload Keyword
;  2004-06-08	 B. Schulz  	ensure that R positive
;===========================================================================
;-

pro bolodarkx_rtstar, x, R_star, T_star,  clabel, Rload=Rload, slope=slope,$
    plot=plot


;------------------------------------------
blo_color_init
nf   = n_elements(x)
npix = n_elements((*x[0]).ubolo[*,0])

R_star = fltarr(npix)
T_star = fltarr(npix)
clabel = strarr(npix)


for ipix = 0, npix-1 do begin

;------------------------------------------
; find R and T at minimum power

  Rmin     = fltarr(nf)
  T_c      = fltarr(nf)

  
  for ifile=0, nf-1 do begin

      if  keyword_set(Rload) then begin
         if keyword_set(slope) then $
             bolodark_r_lowpow2,  (*x[ifile]).ubolo[ipix,*], (*x[ifile]).ubias, $
			 (*x[ifile]).T_c, R, T , RLC=Rload[ipix]$
         else $
             bolodark_r_lowpow, (*x[ifile]).ubolo[ipix,*], (*x[ifile]).ubias, $
			 (*x[ifile]).T_c, R, T,RLC=Rload[ipix]
   
      endif else begin
   
          if keyword_set(slope) then $
            bolodark_r_lowpow2,  (*x[ifile]).ubolo[ipix,*], (*x[ifile]).ubias, $
         			 (*x[ifile]).T_c, R, T $
          else $
            bolodark_r_lowpow, (*x[ifile]).ubolo[ipix,*], (*x[ifile]).ubias, $
         			 (*x[ifile]).T_c, R, T
      endelse
      Rmin[ifile] = abs(R)  &  T_c[ifile]  = T
  endfor

;------------------------------------------
; fit  T_star and R_star and plot fit

  afit = bolodark_fit_rlowp(T_c, Rmin)

  if keyword_set(plot) then begin
    bolodark_pl_rlowp, T_c, Rmin, afit = afit, $
                       title="R at low Power "+(*x[0]).ubolo_label[ipix]
    wait, 0.1
  endif

;------------------------------------------
; save parameters

  clabel[ipix] = (*x[0]).ubolo_label[ipix]
  R_star[ipix] = exp(afit[0])
  T_star[ipix] = afit[1]^2
;------------------------------------------
  print, R_star[ipix], '   ', T_star[ipix]
endfor

return
end
